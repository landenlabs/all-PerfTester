// All-PerfTester
plugins {
    id ("com.android.application")
    id ("org.jetbrains.kotlin.android") // Prevents duplicate kotlin warnings
}

android {

    compileSdk 35
    namespace "com.landenlabs.allperfimages"

    defaultConfig {
        applicationId "com.landenlabs.allperfimages"
        minSdkVersion 23
        targetSdkVersion 35

        versionCode 20630
        versionName "2.06.30"

        // Limit languages to following list.
        resourceConfigurations += ['en', 'es']

        buildConfigField "long", "BuildTimeMilli", System.currentTimeMillis() + "L"

        resValue "string", "targetSdkVersion", targetSdkVersion.apiString
        resValue "string", "compileSdkVersion", compileSdkVersion
        resValue "string", "buildToolsVersion", buildToolsVersion
        // resValue "string", "javaVersion", Jvm.current().javaVersion.toString()
    }

    // Save app version in string so resource can be used in layouts
    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", "v" + variant.versionName
    }

    signingConfigs {
        release
    }

    // Load signing config (does not work for command line builds)
    // println("Setting release=${rootDir}/release.properties")
    if (file("${rootDir}/release.properties").exists()) {
        def props = new Properties()
        props.load(new FileInputStream(file("${rootDir}/release.properties")))
        android.signingConfigs.release.storeFile = file(props.keyStore)
        android.signingConfigs.release.storePassword = props.keyStorePassword
        android.signingConfigs.release.keyAlias = props.keyAlias
        android.signingConfigs.release.keyPassword = props.keyAliasPassword
        println("Setting release to " + signingConfigs.release.storeFile.absolutePath )
    }

    buildFeatures.buildConfig = true

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // Save app version in string so resource can be used in layouts
    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", "version " + variant.versionName
    }

    lint {
        abortOnError false
        disable 'DefaultLocale', 'SimpleDateFormat', 'InflateParams', 'SpUsage',
                'UseCompoundDrawables', 'HandlerLeak', 'ViewConstructor', 'SetTextI18n',
                'RtlSymmetry', 'RtlHardcoded', 'ContentDescription',
                'MissingTranslation', 'ValidFragment', 'UnusedAttribute',
                'FieldCanBeLocal', 'LocalSuppress', 'HardcodedText', 'Typos'
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                // Enable verbose lint reporting during build.
                options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'

    // https://developers.google.com/android/guides/releases
    implementation 'com.google.android.gms:play-services-maps:19.2.0'
}

// Rename app
android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach {
        def ourName = project.parent.name
        outputFileName = "$ourName-${variant.name}-${variant.versionName}.apk"
        println "out name= $outputFileName"
    }
}